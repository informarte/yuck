include "alldifferent.mzn";

int: N = 9;

set of int: STAGES = 1..N;

var STAGES: DomainInitializer;
var STAGES: VariableFactory;
var STAGES: VariableClassifier;
var STAGES: ConstraintFactory;
var STAGES: ObjectiveFactory;
var STAGES: ConstraintNetworkPruner;
var STAGES: Presolver;
var STAGES: NeighbourhoodFactory;
var STAGES: ArrayAccessOptimizer;

constraint alldifferent([
   DomainInitializer, VariableFactory, VariableClassifier, ConstraintFactory,
   ObjectiveFactory, ConstraintNetworkPruner, Presolver, NeighbourhoodFactory,
   ArrayAccessOptimizer]);

/* Sequence of stages to produce a complete model */
constraint DomainInitializer < VariableFactory;
constraint VariableFactory < VariableClassifier;
constraint VariableClassifier < ConstraintFactory;

/* All remaining stages require a complete model */
constraint ConstraintFactory < ObjectiveFactory;
constraint ConstraintFactory < ConstraintNetworkPruner;
constraint ConstraintFactory < Presolver;
constraint ConstraintFactory < NeighbourhoodFactory;
constraint ConstraintFactory < ArrayAccessOptimizer;

/* ObjectiveFactory generates and registers the objective variables */
constraint ObjectiveFactory < ConstraintNetworkPruner;

/* NeighbourhoodFactory departs from the objective produced by ObjectiveFactory */
constraint ObjectiveFactory < NeighbourhoodFactory;

/* Domains must not shrink after identification of implicit constraints */
constraint Presolver < NeighbourhoodFactory;

/* Remove useless constraints before optimizing array access */
constraint ConstraintNetworkPruner < ArrayAccessOptimizer;
